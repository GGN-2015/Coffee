VAR col[9], add[16], sub[16] ; 记录哪些行，哪些列有皇后
VAR ans                      ; 记录总方案数


FUNC printInt(x)
    VAR stk[10], stktop := 0
    IF x = 0 THEN
        PUTCHAR('0')
    ELSE
        WHILE x <> 0 LOOP
            stk[stktop] := x % 10
            x := x / 10
            stktop := stktop + 1
        ENDWHILE
        WHILE stktop > 0 LOOP
            PUTCHAR(stk[stktop - 1] + 48)
            stktop := stktop - 1
        ENDWHILE
    ENDIF
    PUTCHAR(' ')
ENDFUNC


FUNC setQueen(x, i, newVal)
    col[i]         := newVal
    add[x + i - 1] := newVal
    sub[x - i + 8] := newVal
ENDFUNC


FUNC checkPos(x, i)
    RETURN NOT(col[i]) AND NOT(add[x + i - 1]) AND NOT(sub[x - i + 8])
ENDFUNC


FUNC dfs(x)
    IF x >= 9 THEN
        ans := ans + 1
    ELSE
        FOR i := 1 TO 8 DO
            IF checkPos(x, i) THEN
                CALL setQueen(x, i, 1)
                CALL dfs(x + 1)
                CALL setQueen(x, i, 0)
            ENDIF
        ENDFOR
    ENDIF
ENDFUNC


FUNC main()                           ; 一些初始化工作
    CALL dfs(1)
    CALL printInt(ans)
ENDFUNC
